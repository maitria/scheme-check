#!/usr/bin/env gsi
; vim:set ft=scheme:

(define-macro (deprecate legacy-name #!key replacement) 
  `(define (,legacy-name . args)
     (raise ,(string-append "Deprecated. Instead of " (symbol->string legacy-name) " use " (symbol->string replacement)))))

(define (files-in-directory dir)
  (define (full-path-for filename)
    (string-append dir "/" filename))
  (let* ((directory-handle (open-directory `(path: ,dir ignore-hidden: dot-and-dot-dot)))
	 (filenames (read-all directory-handle)))
    (close-input-port directory-handle)

    (map full-path-for filenames)))

(define (items-satisfying item-should-be-included? the-list)
  (define results '())
  (for-each
    (lambda (element)
      (if (item-should-be-included? element)
	(set! results (cons element results))))
    the-list)
  (reverse results))

(deprecate filter replacement: items-satisfying)
;;see deprecate work by uncommenting the following line
;;(write (filter (lambda (x)(< 0 x)) '(1 -2 5 -3)))

(define (last-index-of char str)
  (let loop ((index (- (string-length str) 1)))
    (cond
      ((< index 0)
       #f)
      ((char=? char (string-ref str index))
       index)
      (else
       (loop (- index 1))))))

(define (file-extension filename)
  (let ((dot-index (last-index-of #\. filename))
	(slash-index (last-index-of #\/ filename)))
    (cond
      ((or (not dot-index)
	   (and dot-index
		slash-index
		(> slash-index dot-index)))
       "")
      (else
       (substring filename dot-index (string-length filename))))))

(define (scheme-file? filename)
  (member (file-extension filename) '(".scm" ".ss")))

(define (scheme-files)
  (items-satisfying scheme-file? (files-in-directory ".")))

;; ---------------------------------------------------------------------------
;; CHECKING FILES
;; ---------------------------------------------------------------------------

(define (check-source-file-path)
  (let* ((check-path (this-source-file))
	 (slash-index (last-index-of #\/ check-path)))
    (if slash-index
      (string-append
        (substring check-path 0 (+ 1 slash-index))
        "check-source-file")
      "check-source-file")))

(define system ##shell-command)

(define (check-source-file path)
  (let* ((command (string-append
		    (check-source-file-path)
		    " \""
		    path
		    "\""))
	 (exit-status (system command)))
    (= 0 exit-status)))

(for-each check-source-file (scheme-files))
